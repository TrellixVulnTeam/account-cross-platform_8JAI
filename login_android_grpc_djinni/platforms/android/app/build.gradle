apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.wechat.mylogin"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                // The paths to the protoc and grpc_cpp_plugin binaries on the host system (codegen
                // is not cross-compiled to Android)
                def protoc = project.hasProperty('protoc') ?
                        project.property('protoc') : '/usr/local/bin/protoc'
                def grpc_cpp_plugin = project.hasProperty('grpc_cpp_plugin') ?
                        project.property('grpc_cpp_plugin') : '/usr/local/bin/grpc_cpp_plugin'

                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments '-DANDROID_STL=c++_static'
                arguments '-DRUN_HAVE_POSIX_REGEX=0'
                arguments '-DRUN_HAVE_STD_REGEX=0'
                arguments '-DRUN_HAVE_STEADY_CLOCK=0'
                arguments '-Dprotobuf_BUILD_PROTOC_BINARIES=off'
                arguments '-DgRPC_BUILD_CODEGEN=off'
                arguments '-Dhelloworld_PROTOBUF_PROTOC_EXECUTABLE=' + protoc
                arguments '-Dhelloworld_GRPC_CPP_PLUGIN_EXECUTABLE=' + grpc_cpp_plugin
            }
//            ndk {
//                abiFilters 'armeabi-v7a'
//            }
        }
    }

    signingConfigs {
        myConfig {
            storeFile file("wxlogin.jks")
            storePassword "login2019"
            keyAlias "wxlogin"
            keyPassword "login2019"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.myConfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }



    sourceSets {
        main {
            java {
                srcDirs = [
                        "../../../generated-src/java",
                        "../../../src/java",
                        "src/main/java"
                ]
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
    }

}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.5.1-1' }
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.18.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'

    // You need to build grpc-java to obtain these libraries below.
    implementation 'io.grpc:grpc-okhttp:1.18.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf-lite:1.18.0' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.18.0' // CURRENT_GRPC_VERSION
    implementation 'javax.annotation:javax.annotation-api:1.2'

    implementation 'com.alibaba:fastjson:1.2.44'

    implementation 'io.reactivex.rxjava2:rxjava:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

}